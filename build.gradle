apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'eclipse'

repositories {
  mavenCentral()
}

test {
  useJUnitPlatform()

  // Disable slow tests.
  // TODO: Re-organize project setup so that full tests can be run on-demand in a non-blocking way.
  exclude 'com/edmazur/eqrs/game/CharInfoScraperTest.class'
  exclude 'com/edmazur/eqrs/game/ExpPercentToNextLevelScraperTest.class'
}

dependencies {
  implementation 'com.edmazur.eqlp:everquest-log-parser:0.1.0'
  implementation 'com.github.dikhan:pagerduty-client:3.1.2'
  implementation 'commons-io:commons-io:2.11.0'
  implementation 'mysql:mysql-connector-java:8.0.25'
  implementation 'org.ahocorasick:ahocorasick:0.6.3'
  implementation 'org.javacord:javacord:3.4.0'
  implementation 'org.seleniumhq.selenium:selenium-java:4.0.0'
  testImplementation("org.junit.jupiter:junit-jupiter:5.8.2")
  testImplementation("org.mockito:mockito-core:3.+")
}

mainClassName = 'com.edmazur.eqrs.RobotStanvern'

task runGameTodDetectorBenchmark(type: JavaExec) {
   main = 'com.edmazur.eqrs.game.listener.GameTodDetectorBenchmark'
   classpath = sourceSets.main.runtimeClasspath
}

task runP99WikiItemReader(type: JavaExec) {
   main = 'com.edmazur.eqrs.game.P99WikiItemReader'
   classpath = sourceSets.main.runtimeClasspath
}

task runTickContextAnalysis(type: JavaExec) {
   main = 'com.edmazur.eqrs.game.listener.TickContextAnalysis'
   classpath = sourceSets.main.runtimeClasspath
}

// TODO: Instead of copy/pasting/modifying google_checks.xml into this project,
// see if there's a way to dynamically pull it in (with local config on top of
// it if possible).
checkstyle {
  // This is set to the same version as the Eclipse plugin version so that
  // command-line behavior matches Eclipse behavior.
  toolVersion '9.1'
  maxWarnings 0
}

tasks.register("installGitHooks", Copy) {
  from file("$rootDir/pre-commit")
  into file("$rootDir/.git/hooks")
  fileMode 0775
}
build.dependsOn installGitHooks
